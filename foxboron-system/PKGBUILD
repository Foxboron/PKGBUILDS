# Maintainer: Morten Linderud <morten@linderud.pw>

pkgbase=foxboron-system
pkgname=('base-system'
         'base-packages'
         'base-dotfiles'
         'foxboron-timers'
         'foxboron-backup'
         'patch-system')
pkgver=11
pkgrel=1
pkgdesc="System configs"
arch=("any")
url="https://linderud.dev/"
license=('MIT')
depends=('git')
source=('backup-backup'
        'backup-backup@.service'
        'backup-backup-snapper-cleanup.service'
        'backup-backup-sudo'
        'backup-snap-boot-backup'
        'backup-snap-boot-config'
        'backup-snapper-backup.service'
        'backup-snapper-filter'
        'backup-sudoers'
        'hook-bootctl-upgrade.hook'
        'hook-patch-system-configs.hook'
        'hook-patch-system-configs.scripts'
        'mkinitcpio-linux.preset'
        'kernel-cmdline'
        'pacman-aur.conf'
        'pacman-mirrorlist'
        'snapper-conf.d'
        'snapper-home'
        'snapper-root'
        'system-sudoers'
        'timers-timer@.target'
        'timers-timer@.timer'
        'x11-99-keyboard.conf'
)

sha256sums=('793647362d8c6bbc665dc5a18537c0479f070d0e4453ceff64f8ed8fadab9faa'
            'fba676be6530edb1f4db5bc4bf7ea9ddcd8209822b797bd0f45e7fce5bbb940c'
            '08e1d51e9559f89f8d6a57a3134b05295d8ace56cddac3aa97a0f3792b68ee91'
            'f5252e9184f5469bb7d4091359f752674b5925efe670a19aa65177b7409713b5'
            '48ee59b2d3d2c6839825177a31f7efa6b4f4c7d43139409d63676c7955affb0b'
            '2a978ea589fa3647d6156bdd054c5c76828b8934c832423f9d3a3db9e5e5019a'
            'fde7844e13e00125ad13bd87e0bc3f2347ce7424c054b1e2ade44ea05e1176fd'
            'b17747590458e5e540fad94dc046348f32b4e84565078c5609a824784163f6d2'
            '9eae320f6f0842d0692d46020125432350ed25fbddec2d9829d16ca7f43991c5'
            '3295c32224753a6f1d409b7de810af63c3fd1d987c193486caae84c8b66ba2c2'
            'e03ee1956c15ab4041dc5a29c978d0017ac5d8bce782f813b55bd0193fa7672e'
            '685fb6ce4aec2c8e158fc27c8fb91533e042b00a514af15b45ade43bf2fb07f8'
            'b269081238fe9191dd5b731b60bdbf1e40c0b2929d92c36920a52097f87f0309'
            'ce3abbbc9582db7cce86b589c89125d84d80ffdacdda7f8c110062aa4d523617'
            '7c441eeccea11b5a5ed6c4a4311e33a18930437ba40414289e223612c1d328c9'
            '4f09d8e3cffdd37c5c471873fcbfa5359060ad54c6a0f34df0862dd112c09647'
            '76097926beead2ec82bc0da042ba1d5013d745d3d8d90239e9cecac32b174f6a'
            '408efa4f9eceedcb2a3acffc126f96ffc890abb0bdc55844f50257490d7d18b4'
            'e8b40c151eb20db7cff213de28a63a9f8fa69ba371842ea18fcab11265ed2b87'
            '053e38a80a8162450b9e145c119b9cee0385a869b26d24bb3f681bff78137592'
            '148cffd0796d1fa2b1d9bba9274493bd9cfc2d5a524ffc0e03fc2d6e8e2c53fb'
            'dd01c47408e67c0af174345fb1bd92683d509181a0d8bf8fc1e22ba49e79ec24'
            '477185de77c1cff69e9b7c9e5451fc71294c00c407726f87e29324f34379c01f'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
            'b28f6a2b15618d35b6436f7c78f75c84daf6177b74bace6770481535d78fca8d'
            'c80d58d30bf231bb696cabf792f6959dce52083209c99f0057334b1871500251'
            '0e640b1d5e9a154c194233f9e4edc1e456c5062d4303785b30eb70d708ecacc0')

package_base-packages(){
    depends=(
        'base'
        'linux' 'linux-firmware'
        'mkinitcpio'

        # xorg
        'xorg-server' 'xorg-setxkbmap' 'xorg-xinit' 'xorg-xset'
        'intel-ucode' 'xorg-xrandr'

        'grobi' 'ddcutil'

        # DE
        'i3-gaps' 'xorg-xset' 'i3lock' 'redshift' 'alacritty' 'feh'
        'dunst' 'redshift' 'playerctl' 'noto-fonts' 'unclutter'
        'picom' 'rofi' 'ttf-dejavu' 'xdotool' 'i3blocks'
        'pavucontrol' 'qutebrowser' 'chromium' 'xss-lock'

        # System
        'tlp' 'udiskie' 'usbguard' 'lm_sensors'
        'networkmanager' 'zsh' 'fwupd'
        'bolt' 'wireguard-tools' 'pipewire'
        'pipewire-jack' 'pipewire-pulse' 'pipewire-media-session'

        'sbctl'

        # Utilities
        'gnupg' 'pass' 'mpv' 'expac' 'xsel'
        'zathura-pdf-mupdf' 'zathura' 'imv' 'mosh' 'openssh'
        'htop' 'task' 'yubikey-manager' 'pass-otp'
        'nvchecker' 'tmux' 'tig' 'git' 'pacman-contrib'
        'hub' 'darktable' 'github-cli' 'podman' 'docker'
        'pacutils' 'pcsclite' 'ccid' 'lxd' 'libfido2' 'efibootmgr'

        'bluez' 'bluez-utils'

        # Editor
        'gvim' 'ctags' 'fzf' 'ripgrep'

        'neomutt' 'notmuch' 'afew' 'msmtp'

        # latex
        # 'biber' 'texlive-bibtexextra' 'texlive-core'
        # 'texlive-fontsextra' 'texlive-latexextra'
        # 'zathura' 'zathura-pdfp-mupdf'
        )
}


package_base-dotfiles(){
    git clone 'https://github.com/foxboron/home.git' "$pkgdir"/etc/skel
    mv "$pkgdir"/etc/skel/.git "$pkgdir"/etc/skel/.config/home.git
}

package_base-system(){
    install="$pkgname.install"
    depends=('base-packages' 'base-dotfiles')
    install -Dm0644 pacman-mirrorlist "$pkgdir/usr/share/system-config/pacman/mirrorlist"
    install -Dm0644 kernel-cmdline "$pkgdir/etc/kernel/cmdline"
    install -Dm0644 mkinitcpio-linux.preset "$pkgdir/etc/mkinitcpio.d/linux.preset"
    install -Dm0644 hook-bootctl-upgrade.hook "$pkgdir/etc/pacman.d/hooks/100-bootctl-upgrade.hook"
    install -Dm0644 pacman-aur.conf  "$pkgdir"/etc/pacman.d/aur.conf
    install -Dm0644 x11-99-keyboard.conf "$pkgdir"/etc/X11/xorg.conf.d/99-keyboard.conf
    install -dm0750 "$pkgdir"/etc/sudoers.d
    install -Dm0644 system-sudoers "$pkgdir"/etc/sudoers.d/10-system
    ln -sf /usr/share/zoneinfo/Europe/Oslo "$pkgdir"/etc/localtime
}


_patch_system=(*.patch)
source+=("${_patch_system[@]}")
package_patch-system(){
    install -Dm0644 hook-patch-system-configs.hook "$pkgdir"/usr/share/libalpm/hooks/90-patch-system-configs.hook
    install -Dm0755 hook-patch-system-configs.scripts "$pkgdir"/usr/share/libalpm/scripts/patch-system-configs
    for p in ${_patch_system[@]}; do
        install -Dm0644 "$p" "$pkgdir/usr/share/system-config/patches/${p#patch-}"
    done
}

package_theia-system(){
    depends=('base-system' 'acpid')
    install -Dm0644 theia-capslock.hwdb "$pkgdir"/etc/udev/hwdb.d/61-theia-capslock.hwdb
    install -Dm0644 theia-thinkfan.conf "$pkgdir"/etc/thinkfan.conf
    install -Dm0644 theia-thinkpad_acpi.conf "$pkgdir"/etc/modprobe.d/thinkpad_acpi.conf
    install -Dm0644 acpid-thinkpad-dock-lenovo "$pkgdir"/etc/acpi/events/thinkpad-dock-lenovo
    install -Dm0644 acpid-thinkpad-undock-lenovo "$pkgdir"/etc/acpi/events/thinkpad-undock-lenovo
    install -Dm0755 acpid-thinkpad-undock.sh "$pkgdir"/etc/acpi/thinkpad-undock.sh
    install -Dm0755 acpid-thinkpad-dock.sh "$pkgdir"/etc/acpi/thinkpad-dock.sh
    install -Dm0644 x11-99-keyboard.conf "$pkgdir"/etc/X11/xorg.conf.d/99-keyboard.conf
    install -Dm0644 x11-99-touchpad-disable.conf "$pkgdir"/etc/X11/xorg.conf.d/99-touchpad-disable.conf
    printf 'theia\n' | install -Dm0644 /dev/stdin "$pkgdir"/etc/hostname
}

package_anathema-system(){
    depends=('base-system')
    install -Dm0644 x11-99-keyboard.conf "$pkgdir"/etc/X11/xorg.conf.d/99-keyboard.conf
    install -Dm0644 x11-99-touchpad-disable.conf "$pkgdir"/etc/X11/xorg.conf.d/99-touchpad-disable.conf
    install -Dm0644 udev-95-thunderbolt.rules "$pkgdir"/etc/udev/rules.d/95-thunderbolt.rules
    install -Dm0755 udev-hotplug_monitor "$pkgdir"/usr/bin/hotplug_monitor
    printf 'anathema\n' | install -Dm0644 /dev/stdin "$pkgdir"/etc/hostname
}

package_foxboron-timers(){
    install=timers.install
    for p in timers-*; do
        install -Dm0644 "$p" "$pkgdir/usr/lib/systemd/system/${p#timers-}"
        install -Dm0644 "$p" "$pkgdir/usr/lib/systemd/user/${p#timers-}"
    done
}

package_foxboron-backup(){
    # This really needs to be factored so we dont rely on bad dotfiles
    install=snapper.install
    depends=('foxboron-timers' 'snapper' 'expac')
    install -Dm0644 backup-backup@.service "$pkgdir"/usr/lib/systemd/system/backup@.service
    install -Dm0644 backup-snapper-backup.service "$pkgdir"/usr/lib/systemd/system/snapper-backup.service
    install -dm0750 "$pkgdir"/etc/sudoers.d
    install -Dm0644 backup-sudoers "$pkgdir"/etc/sudoers.d/20-backup
    install -Dm0755 backup-backup "$pkgdir"/usr/bin/backup
    install -Dm0755 backup-backup-sudo "$pkgdir"/usr/bin/backup-sudo
    install -Dm0644 snapper-home "$pkgdir"/etc/snapper/configs/home
    install -Dm0644 snapper-root "$pkgdir"/etc/snapper/configs/root
    install -Dm0644 snapper-conf.d "$pkgdir"/etc/conf.d/snapper
}

package_foxboron-pacman-backup(){
    install -Dm0755 backup-snap-boot-backup "$pkgdir"/usr/bin/snap-boot-back
    install -Dm0644 backup-50_bootbackup.hook "$pkgdir"/usr/share/libalpm/hooks/50_bootbackup.hook
    install -Dm0644 backup-zz_bootback.hook "$pkgdir"/usr/share/libalpm/hooks/zz_bootback.hook
    install -Dm0644 backup-snap-boot-config "$pkgdir"/usr/share/$pkgname/snap-boot.config
}
